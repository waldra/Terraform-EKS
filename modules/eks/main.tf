# EKS Cluster
resource "aws_eks_cluster" "eks" {
  name     = var.cluster_name
  version  = var.cluster_version
  role_arn = aws_iam_role.eks_cluster_role.arn

  vpc_config {
    subnet_ids         = var.public_subnets_ids
    security_group_ids = [var.eks_sg_id]
  }
}

# Assoicate OIDC Identity Provider to the EKS Cluster
resource "aws_iam_openid_connect_provider" "oidc" {
  client_id_list  = ["sts.amazonaws.com"]
  thumbprint_list = ["9e99a48a9960d50a560f3598d2f9c7e9c6c9c165"] # Default thumbprint for AWS OIDC
  url             = aws_eks_cluster.eks.identity[0].oidc[0].issuer
}

# ESK Node group
resource "aws_eks_node_group" "eks_node_group" {
  cluster_name    = aws_eks_cluster.eks.name
  node_group_name = var.node_group_name
  node_role_arn   = aws_iam_role.node_group_role.arn
  subnet_ids      = var.private_subnets_ids

  scaling_config {
    desired_size = 2
    max_size     = 4
    min_size     = 2
  }

  instance_types = ["t2.micro"]

  remote_access {
    ec2_ssh_key               = var.ssh_key_name
    source_security_group_ids = [var.eks_sg_id]
  }

}

# IAM Role for EKS cluster
resource "aws_iam_role" "eks_cluster_role" {
  name = "eks-cluster-role"

  assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Principal": {
        "Service": "eks.amazonaws.com"
      },
      "Action": "sts:AssumeRole"
    }
  ]
}
EOF

}

# Attach Policies to EKS IAM Role
resource "aws_iam_role_policy_attachment" "eks_cluster_role_policy" {
  for_each = toset([
    "arn:aws:iam::aws:policy/AmazonEKSClusterPolicy",
    "arn:aws:iam::aws:policy/AmazonEKSVPCResourceController"
  ])
  role       = aws_iam_role.eks_cluster_role.name
  policy_arn = each.value
}

# IAM Role for node group
resource "aws_iam_role" "node_group_role" {
  name = "eks-node-group-role"

  assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Principal": {
        "Service": "ec2.amazonaws.com"
      },
      "Action": "sts:AssumeRole"
    }
  ]
}
EOF

}

# Attach Policies to the IAM Role of EKS node group
resource "aws_iam_role_policy_attachment" "node_group_policies" {
  for_each = toset([
    "arn:aws:iam::aws:policy/AmazonEKSWorkerNodePolicy",
    "arn:aws:iam::aws:policy/AmazonEKS_CNI_Policy",
    "arn:aws:iam::aws:policy/AmazonEC2ContainerRegistryReadOnly"
  ])
  role       = aws_iam_role.node_group_role.name
  policy_arn = each.value

}

/* EKS Add-ons
resource "aws_eks_addon" "vpc_cni" {
  for_each     = toset(["vpc-cni", "CoreDNS", "kube-proxy"])
  addon_name   = each.value
  cluster_name = aws_eks_cluster.eks.name

}
*/